technician = {
    category = worker
    condition_string = WORKER_JOB_TRIGGER
    building_icon = building_energy_grid
    clothes_texture_index = 2
    possible_pre_triggers = {
        has_owner = yes
        is_being_purged = no
        is_being_assimilated = no
        is_sapient = yes
    }
    possible_precalc = can_fill_worker_job
    possible = {
        hidden_trigger = {
            exists = owner
        }
        owner = {
            is_gestalt = no
        }
    }
    resources = {
        category = planet_technician
        produces = {
            energy = 6
        }
        produces = {
            trigger = {
                exists = owner
                owner = {
                    is_robot_empire = yes
                }
            }
            energy = 2
        }
        produces = {
            trigger = {
                planet = {
                    has_planet_flag = has_energy_requisitorium
                }
            }
            energy = @requisitorium_energy_reduction
        }
    }
    overlord_resources = {
        category = planet_requisitioned_energy
        produces = {
            trigger = {
                planet = {
                    has_planet_flag = has_energy_requisitorium
                }
            }
            energy = @requisitorium_energy_overlord
        }
    }
    weight = {
        weight = @worker_job_weight
        mult = value:job_weights_modifier|JOB|technician|RESOURCE|energy|
        modifier = {
            factor = 100
            OR = {
                is_non_sapient_robot = yes
                is_shackled_robot = yes
            }
            can_take_servant_job = no
            can_fill_specialist_job_trigger = no
        }
        modifier = {
            factor = 8
            is_enslaved = yes
            can_take_servant_job = no
            OR = {
                has_trait = trait_syncretic_proles
                has_trait = trait_robot_superconductive
                has_trait = trait_ingenious
            }
        }
        modifier = {
            factor = 2
            OR = {
                is_non_sapient_robot = yes
                is_shackled_robot = yes
            }
            can_take_servant_job = no
        }
        modifier = {
            factor = 0.25
            can_take_servant_job = yes
        }
        modifier = {
            factor = 250
            exists = planet
            planet = {
                has_available_jobs = "technician"
            }
            exists = owner
            owner = {
                is_ai = yes
                has_monthly_income = {
                    resource = energy
                    value < 20
                }
            }
        }
    }
}
