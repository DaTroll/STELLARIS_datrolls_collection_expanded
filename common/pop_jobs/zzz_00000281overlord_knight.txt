overlord_knight = {
    category = specialist
    can_set_priority = no
    building_icon = building_order_keep
    clothes_texture_index = 3
    icon = knight
    possible_pre_triggers = {
        has_owner = yes
        is_being_purged = no
        is_being_assimilated = no
    }
    possible_precalc = can_fill_specialist_job
    possible = {
        hidden_trigger = {
            exists = owner
        }
        owner = {
            is_gestalt = no
        }
    }
    overlord_resources = {
        category = planet_knights
        produces = {
            unity = 2
            mult = value:knights_unity_output_modifier
        }
        produces = {
            physics_research = 2
            engineering_research = 2
            society_research = 2
            mult = value:knights_research_output_modifier
        }
    }
    resources = {
        category = planet_knights
        upkeep = {
            alloys = 1
            trigger = {
                exists = owner.overlord
                owner.overlord = {
                    NOT = {
                        has_modifier = luminous_blades
                    }
                }
            }
        }
    }
    pop_modifier = {
        pop_defense_armies_add = 2
    }
    planet_modifier = {
        planet_stability_add = 3
    }
    weight = {
        weight = @overlord_job_weight
        mult = value:job_weights_modifier|JOB|overlord_knight|RESOURCE|unity|
        mult = value:job_weights_research_modifier|JOB|overlord_knight|
        modifier = {
            factor = 0
            is_enslaved = yes
        }
        modifier = {
            factor = 5
            has_job = overlord_knight
        }
    }
}
