namespace = distar
ship_event = {
    id = distar.3020
    title = "distar.3020.name"
    desc = "distar.3020.desc"
    picture = GFX_evt_exploding_ship
    show_sound = event_scanner
    location = from
    is_triggered_only = yes
    immediate = {
        save_event_target_as = stranded_scientist_science_ship
        if = {
            limit = {
                owner_species = {
                    species_portrait = human
                }
            }
            create_species = {
                name = random
                class = HUM
                portrait = "humanoid_05"
                namelist = "HUM2"
                traits = random
                effect = {
                    save_event_target_as = stranded_scientist_species
                }
            }
        }
        else = {
            create_species = {
                name = PRESCRIPTED_species_name_humans1
                class = HUM
                portrait = "human"
                namelist = "HUMAN1"
                traits = {
                    trait = "trait_adaptive"
                    trait = "trait_nomadic"
                    trait = "trait_wasteful"
                }
                effect = {
                    save_event_target_as = stranded_scientist_species
                }
            }
        }
        create_country = {
            name = random
            type = faction
            flag = {
                icon = {
                    category = "special"
                    file = "unknown.dds"
                }
                background = {
                    category = "backgrounds"
                    file = "00_solid.dds"
                }
                colors = {
                    "blue"
                    "blue"
                    "null"
                    "null"
                }
            }
            effect = {
                save_event_target_as = stranded_scientist_country
            }
        }
        event_target:stranded_scientist_country = {
            create_leader = {
                class = scientist
                species = event_target:stranded_scientist_species
                name = random
                skill = 6
                event_leader = yes
                leader_age_min = 30
                leader_age_max = 50
                traits = {
                    trait = "leader_trait_expertise_computing"
                    trait = "leader_trait_spark_of_genius"
                }
            }
        }
        last_created_leader = {
            set_is_female = yes
            save_event_target_as = stranded_scientist_leader
            exile_leader_as = stranded_scientist_leader
        }
    }
    option = {
        name = distar.3020.a
        custom_tooltip = distar.3020.a.tooltip
        hidden_effect = {
            owner = {
                country_event = {
                    id = distar.3021
                    days = 60
                }
                clone_leader = {
                    target = event_target:stranded_scientist_leader
                    class = scientist
                    effect = {
                        save_event_target_as = stranded_scientist_leader_clone
                    }
                }
            }
            event_target:stranded_scientist_leader = {
                kill_leader = {
                    type = scientist
                    show_notification = no
                }
            }
        }
    }
    option = {
        name = distar.3020.b
        custom_tooltip = distar.3020.b.tooltip
        random_list = {
            1 = {
                leader = {
                    add_trait = leader_trait_adaptable
                }
            }
            1 = {
                leader = {
                    add_trait = leader_trait_paranoid
                }
            }
        }
        hidden_effect = {
            event_target:stranded_scientist_leader = {
                kill_leader = {
                    type = scientist
                    show_notification = no
                }
            }
        }
    }
}